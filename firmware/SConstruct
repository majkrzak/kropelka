from math import log

from cc1101 import CC1101

name = "kropelka"

cc1101 = CC1101(
        oscilator_frequency = 26_000_000,
        oscilator_error = 32.5 / 1_000_000,
        carier_frequency = 433_920_000,
        data_rate = 9600
    )

print(cc1101)


env = Environment(
    CC="avr-gcc",
    OBJCOPY="avr-objcopy",
    CCFLAGS=[
        "-Os",
        "-Wall",
        "-Wextra",
        "-std=c11",
        "-mmcu=attiny13a",
    ],
    CPPDEFINES={
        "F_CPU": 9600000,
        "RADIO_FREQ": ",".join(map(str, cc1101.FREQ)),
        "RADIO_DRATE_E": cc1101.DRATE_E,
        "RADIO_DRATE_M": cc1101.DRATE_M,
        "RADIO_CHANSPC_E": cc1101.CHANSPC_E,
        "RADIO_CHANSPC_M": cc1101.CHANSPC_M,
        "RADIO_CHANBW_E": cc1101.CHANBW_E,
        "RADIO_CHANBW_M": cc1101.CHANBW_M,
    },
    BUILDERS={
        "Hex": Builder(
            action="$OBJCOPY -j .text -j .data -O ihex $SOURCE $TARGET",
            suffix=".hex",
            single_source=True,
        )
    },
    PROGSUFFIX=".elf",
)


env.Hex(env.Program(name, [env.Object(s) for s in env.Glob("*.c")]))
